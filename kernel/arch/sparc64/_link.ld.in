/** SPARC64 linker script
 *
 *  It is ELF format, but its only section looks like this:
 *   kernel text
 *   kernel data
 *
 */

#include <arch/boot/boot.h>

ENTRY(kernel_image_start)

SECTIONS {
	.image VMA: AT (LMA) {
		ktext_start = .;
		*(K_TEXT_START)
		*(.text .text.*);
		ktext_end = .;

		kdata_start = .;
		*(K_DATA_START)
		*(.rodata .rodata.*);
		*(.data);                   /* initialized data */
		*(.sdata);
		*(.sdata2);
		*(.sbss);
		. = ALIGN(8);
		hardcoded_ktext_size = .;
		QUAD(ktext_end - ktext_start);
		hardcoded_kdata_size = .;
		QUAD(kdata_end - kdata_start);
		hardcoded_load_address = .;
		QUAD(VMA);
		*(.bss);                    /* uninitialized static variables */
		*(COMMON);                  /* global variables */

		. = ALIGN(8);
		symbol_table = .;
		*(symtab.*);                /* Symbol table, must be LAST symbol!*/

		kdata_end = .;
	}

	/DISCARD/ : {
		*(*);
	}

}
