/*
 * RISC-V 64 linker script
 *
 *  kernel text
 *  kernel data
 *
 */

OUTPUT_ARCH(riscv)
ENTRY(kernel_image_start)

#include <arch/boot/boot.h>
#include <arch/mm/page.h>

SECTIONS {
	kernel_load_address = PA2KA(BOOT_OFFSET);

	.image (PA2KA(BOOT_OFFSET) + SIZEOF_HEADERS) : AT (SIZEOF_HEADERS) {
		ktext_start = .;
		*(K_TEXT_START);
		*(.text);
		ktext_end = .;

		kdata_start = .;
		*(.data);                       /* initialized data */
		*(.rodata*);
		*(.sdata);
		*(.reginfo);
		*(.sbss);
		*(.scommon);
		*(.bss);                        /* uninitialized static variables */
		*(COMMON);                      /* global variables */
		. = ALIGN(8);
		symbol_table = .;
		*(symtab.*);
		kdata_end = .;
	}

	/DISCARD/ : {
		*(.mdebug*);
		*(.pdr);
		*(.comment);
		*(.note);
	}
}
